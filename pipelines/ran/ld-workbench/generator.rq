PREFIX dc:      <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX edm:     <http://www.europeana.eu/schemas/edm/>
PREFIX ore:     <http://www.openarchives.org/ore/terms/>
PREFIX sdo:     <http://schema.org/>
PREFIX doap:    <http://usefulinc.com/ns/doap#>
PREFIX svcs:    <http://rdfs.org/sioc/services#>

CONSTRUCT {

    $this a edm:ProvidedCHO ;
		dc:identifier ?identifier ;
		edm:pid ?pid ;		
		dc:type ?genre ;
		dcterms:spatial ?spatial ;
		dc:subject ?subject ;
		dcterms:created ?created ;
		dcterms:isPartOf ?isPartOf ;
		dc:title ?name ;
		dc:rights ?rights ;
		dc:creator ?creator ;
		dc:description ?description ;
		edm:type ?edm_type ;
	.
		
	?uri_ore a ore:Aggregation ;
		edm:aggregatedCHO $this ;
		edm:isShownAt ?isShownAt ;
		edm:hasView ?isShownBy ;
		edm:rights ?license ;
		edm:dataProvider ?dataProvider ;
		edm:provider ?provider ;
	.

	?isShownBy a edm:WebResource ;
		svcs:has_service ?uri_iiif ;
	.

	?uri_iiif a svcs:Service ;
		dcterms:conformsTo <http://iiif.io/api/image> ;
		doap:implements <http://iiif.io/api/image/3/level2.json> ;
	.

} WHERE {
	
	# Provided CHO

	BIND(("IMAGE") as ?edm_type)

	# er wordt ^^<http://www.w3.org/1999/02/22-rdf-syntax-ns#XMLLiteral> gebruikt, vandaar de STR() her en der

	$this sdo:identifier ?_identifier .
	BIND(STR(?_identifier) AS ?identifier) 

	OPTIONAL {
		$this sdo:genre ?genre .
	}
	OPTIONAL {
		$this sdo:contentLocation ?spatial .
	}
	OPTIONAL {
	    $this sdo:about ?_subject .
	    BIND(
            IF(REGEX(STR(?_subject), "^https?://"),
               IRI(STR(?_subject)), # Cast to IRI
               STR(?_subject)       # Cast to simple literal (xsd:string)
            ) AS ?subject)
	}
	OPTIONAL {
	   $this sdo:abdateCreatedout ?created 
	}
	OPTIONAL {
	    $this sdo:isPartOf ?_isPartOf .
        BIND(
            IF(REGEX(STR(?_isPartOf), "^https?://"),
               IRI(STR(?_isPartOf)),                  # Cast to IRI
               STRLANG(STR(?_isPartOf), "nl")         # Cast to literal with language tag "nl"
            ) AS ?isPartOf)
	}
	$this sdo:name ?_name .
	BIND(STRLANG(STR(?_name), "nl") AS ?name) 

	OPTIONAL {
		$this sdo:copyrightHolder ?rights 
	}
	OPTIONAL {
		$this sdo:creator ?creator 
	}
	OPTIONAL {
		$this sdo:comment ?description 
	}

	# ore:Aggregation

	BIND(URI(CONCAT(STR($this),"#agg")) as ?uri_ore)
	BIND(STR('DC4EU') as ?provider)
	BIND(("Regionaal Archief Nijmegen") as ?dataProvider)
	
	$this sdo:url ?isShownAt .
	$this sdo:url ?pid .
	$this sdo:image ?isShownBy . 
	$this sdo:license ?license .
	
	FILTER(?license != <http://rightsstatements.org/vocab/InC/1.0/> )

    # https://studiezaal.nijmegen.nl/HttpHandler//icoon.ico?file=13666470
	# >
	# https://studiezaal.nijmegen.nl/IIIF/image/3.0/13666470/full/max/0/default.jpg
	BIND(IRI(CONCAT("https://studiezaal.nijmegen.nl/IIIF/image/3.0/",REPLACE(STR(?isShownBy), "^.*file=([0-9]+)$", "$1"))) AS ?uri_iiif)

}